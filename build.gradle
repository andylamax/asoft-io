buildscript {
    ext {
        kotlin_version = "1.3.61"
        android_tools_version = "3.5.2"
        coroutines_version = "1.3.3"
        serialization_version = "0.14.0"
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:$android_tools_version")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("org.jetbrains.kotlin:kotlin-serialization:$kotlin_version")
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.61'
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://jitpack.io" }
}

group 'tz.co.asoft'
version '3.0.0'

apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java.srcDirs += 'src/androidMain/kotlin'
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    js() {
        browser()
        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = 'commonjs'
            }
        }
    }

    android {
        publishLibraryVariants("release")
    }

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        androidMain {
            dependencies {
                api kotlin('stdlib')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }

        jvmMain {
            dependencies {
                api kotlin('stdlib')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependencies {
                api kotlin('stdlib-js')
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}